PREFIX pmid: <https://pubmed.ncbi.nlm.nih.gov/>
PREFIX EDAM: <http://edamontology.org/>
PREFIX BIOLINK: <https://w3id.org/biolink/vocab/>
PREFIX ENSEMBL: <http://identifiers.org/ensembl/>
PREFIX NCBIGENE: <http://identifiers.org/ncbigene/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT ?geneIdentifier ?logFoldChange ?pValue
WHERE {
  VALUES ?pmid_uri { pmid:33262327 }  # <-- CHANGE THIS ID

  ?pmid_uri EDAM:has_output ?dataset .
  ?dataset EDAM:has_output ?row .

  ?row EDAM:data_1669 ?pValue .
  ?row EDAM:data_3754 ?logFoldChange .

  OPTIONAL { ?row BIOLINK:Gene ?geneURI . }
  OPTIONAL { ?row ENSEMBL:id ?geneENS . }
  OPTIONAL { ?row NCBIGENE:id ?geneNCBI . }
  OPTIONAL { ?row BIOLINK:symbol ?geneSYM . }

  BIND(COALESCE(?geneURI, ?geneENS, ?geneNCBI, ?geneSYM) AS ?geneIdentifier)

  FILTER(?pValue < 0.05 && ABS(?logFoldChange) > 1.0)
}
ORDER BY DESC(ABS(?logFoldChange))